{

一番シンプルなToDoListの作成：
Form1にMemo1、
Edit1、Button1（Edit1の内容をMemo1にAppend）、
Button2(ファイルにMemo1の内容を書き込み)、
Button3（ファイルからMemo1に内容を読み込み）

※Memo1のインデックスについて、１行目を0と数える。２行目が1。このずれに注意すること。

ファイル関係

1. ファイル変数の定義	var F: TextFile;
2. ファイル名の定義	var FileName: string;
3. 行データの定義	var text_data: string;

4. ファイルの関連付け	AssignFile(F, FileName);
5. 新規作成or上書き	Rewrite(F);
6. ファイルの読み取り	Reset(F);
7. ファイルへの追加 Append(F);
8. ファイルの書き込み	Writeln(F, text_data);
9. ファイルの閉鎖	CloseFile(F);

}

unit Unit1;

interface

uses
  Classes, SysUtils, FileUtil, Forms, Controls, Graphics, Dialogs, StdCtrls;

type

  { TForm1 }

  TForm1 = class(TForm)
    Button1: TButton;
    Button2: TButton;
    Button3: TButton;
    Edit1: TEdit;
    Memo1: TMemo;
    procedure Button1Click(Sender: TObject);
    procedure Button2Click(Sender: TObject);
    procedure Button3Click(Sender: TObject);
  private

  public

  end;

var
  Form1: TForm1;

implementation

{$R *.lfm}

{ TForm1 }

procedure TForm1.Button1Click(Sender: TObject);
begin
  if Edit1.Text <> '' then
  begin
    Memo1.Lines.Append(Edit1.Text);
    Edit1.Clear;
  end
  else
    ShowMessage('ToDoを入力してください。');
end;

procedure TForm1.Button2Click(Sender: TObject);
var
  FileName: string;
  F: TextFile;
  i: Integer;
begin
  FileName := 'data.txt';
  if not FileExists(FileName) then
  begin
    if MessageDlg('ファイルが存在しません。新しくファイルを作成しますか？', mtConfirmation, [mbYes, mbNo], 0) = mrYes then
    begin
      AssignFile(F, FileName);
      Rewrite(F);
      CloseFile(F);
    end
    else
      Exit;
  end;

  AssignFile(F, FileName);
  Rewrite(F);
  try
    for i := 0 to Memo1.Lines.Count - 1 do
    begin
      Writeln(F, Memo1.Lines[i]);
    end;
  finally
    CloseFile(F);
  end;

  ShowMessage('ToDoリストが保存されました。');
end;

procedure TForm1.Button3Click(Sender: TObject);
var
  FileName: string;
  F: TextFile;
  Line: string;
begin
  FileName := 'data.txt';
  if FileExists(FileName) then
  begin
    AssignFile(F, FileName);
    Reset(F);
    try
      Memo1.Lines.Clear;
      while not Eof(F) do
      begin
        Readln(F, Line);
        Memo1.Lines.Add(Line);
      end;
    finally
      CloseFile(F);
    end;
  end
  else
    ShowMessage('ファイルが存在しません。');
end;

end.
